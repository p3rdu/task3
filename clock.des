INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI

    root = Tk()
    root.title("A Title")
    g = DWatchGUI(root, eventhandler)

    eventhandler.start()

    root.mainloop()
    exit()

STATECHART:
    A [CS][DS] 
     CLOCKTICKING [DS]
     SETTIMEBUTTONKEPTPRESSED
     SETHOURS
     SETMINUTES
     SETSECONDS
    B [CS][DS]
     LIGHTOFF [DS]
     LIGHTONBUTTONKEPTPRESSED 
     LIGHTONBUTTONRELEASED
    C [CS][DS]
     TIMEDISPLAY [DS]
     ALARMDISPLAY
     ALARMBUTTONKEPTPRESSED
     SETALARMHOURS
     SETALARMMINUTES
     SETALARMSECONDS
     CHRONODISPLAY
    D [CS][DS]
     CHRONOSTOPPED [DS]
     CHRONORUNNING 
         
    
TRANSITION:
    S: B.LIGHTOFF
    N: B.LIGHTONBUTTONKEPTPRESSED
    E: lightOn
    O: print "set light on"
       g.setIndiglo()

TRANSITION:
    S: B.LIGHTONBUTTONKEPTPRESSED
    N: B.LIGHTONBUTTONRELEASED
    E: lightOff
    O: print "light goes off after 2 sec"

TRANSITION:
    S: B.LIGHTONBUTTONRELEASED
    N: B.LIGHTOFF
    T: 2  # by default, it is [RTT]
    O: print "light off"
       g.unsetIndiglo()       
       
TRANSITION:
    S: A.CLOCKTICKING
    N: A.CLOCKTICKING
    T: 1  # by default, it is [RTT]
    O: g.increaseTimeByOne()
    
TRANSITION:
    S: A.CLOCKTICKING
    N: A.SETTIMEBUTTONKEPTPRESSED
    C: [INSTATE("C.TIMEDISPLAY")]
    E: initChrono
    O: print "bottom right pressed"
    
TRANSITION:
    S: A.SETTIMEBUTTONKEPTPRESSED
    N: A.CLOCKTICKING
    E: released
    O: print "bottom right released"            
    
TRANSITION:
    S: A.SETTIMEBUTTONKEPTPRESSED
    N: A.SETHOURS
    T: 1.5  # by default, it is [RTT]
    O: g.startSelection()
    
TRANSITION:
    S: A.SETHOURS
    N: A.SETHOURS
    E: increase
    O: g.increaseSelection()
    
TRANSITION:
    S: A.SETHOURS
    N: A.SETMINUTES
    E: editTime
    O: g.selectNext()

TRANSITION:
    S: A.SETMINUTES
    N: A.SETMINUTES
    E: increase
    O: g.increaseSelection() 
    
TRANSITION:
    S: A.SETMINUTES
    N: A.SETSECONDS
    E: editTime
    O: g.selectNext()

TRANSITION:
    S: A.SETSECONDS
    N: A.SETSECONDS
    E: increase
    O: g.increaseSelection()
    
TRANSITION:
    S: A.SETSECONDS
    N: A.CLOCKTICKING
    T: 5
    O: g.stopSelection()
       g.refreshTimeDisplay()

TRANSITION:
    S: A.SETMINUTES
    N: A.CLOCKTICKING
    T: 5
    O: g.stopSelection()
       g.refreshTimeDisplay()

TRANSITION:
    S: A.SETHOURS
    N: A.CLOCKTICKING
    T: 5
    O: g.stopSelection() 
       g.refreshTimeDisplay()
       
TRANSITION:
    S: C.TIMEDISPLAY
    N: C.TIMEDISPLAY
    T: 1
    O: g.refreshTimeDisplay()       

TRANSITION:
    S: C.CHRONODISPLAY
    N: C.CHRONODISPLAY
    T: 0.01
    O: g.refreshChronoDisplay()
           
TRANSITION:
    S: C.TIMEDISPLAY
    N: C.CHRONODISPLAY
    E: changeMode
    O: g.refreshChronoDisplay()
    
TRANSITION:
    S: C.TIMEDISPLAY
    N: C.ALARMBUTTONKEPTPRESSED
    C: [INSTATE("A.CLOCKTICKING")]
    E: setAlarm
    O: g.refreshAlarmDisplay()
    
TRANSITION:
    S: C.ALARMBUTTONKEPTPRESSED
    N: C.SETALARMHOURS
    T: 1.5
    O: g.startSelection()
    
TRANSITION:
    S: C.SETALARMHOURS
    N: C.SETALARMHOURS
    E: increase
    O: g.increaseSelection()

TRANSITION:
    S: C.SETALARMHOURS
    N: C.SETALARMMINUTES
    E: editTime
    O: g.selectNext()   

TRANSITION:
    S: C.SETALARMMINUTES
    N: C.SETALARMMINUTES
    E: increase
    O: g.increaseSelection()

TRANSITION:
    S: C.SETALARMMINUTES
    N: C.SETALARMSECONDS
    E: editTime
    O: g.selectNext() 
        
TRANSITION:
    S: C.SETALARMSECONDS
    N: C.SETALARMSECONDS
    E: increase
    O: g.increaseSelection()           
          
TRANSITION:
    S: C.SETALARMHOURS
    N: C.ALARMDISPLAY
    T: 5
    O: g.stopSelection()
       g.refreshAlarmDisplay()
       
TRANSITION:
    S: C.SETALARMMINUTES
    N: C.ALARMDISPLAY
    T: 5
    O: g.stopSelection()
       g.refreshAlarmDisplay()  

TRANSITION:
    S: C.SETALARMSECONDS
    N: C.ALARMDISPLAY
    T: 5
    O: g.stopSelection()
       g.refreshAlarmDisplay() 
          
TRANSITION:
    S: C.ALARMBUTTONKEPTPRESSED
    N: C.ALARMDISPLAY
    E: onoff
    O: g.refreshAlarmDisplay()    
    
TRANSITION:
    S: C.ALARMDISPLAY
    N: C.TIMEDISPLAY
    E: setAlarm
    O: g.refreshAlarmDisplay()         

TRANSITION:
    S: C.CHRONODISPLAY
    N: C.TIMEDISPLAY
    E: changeMode
    O: g.refreshTimeDisplay()
    
TRANSITION:
    S: D.CHRONOSTOPPED
    N: D.CHRONORUNNING
    C: [INSTATE("C.CHRONODISPLAY")]
    E: initChrono
    O: print "Start running chrono" 

TRANSITION:
    S: D.CHRONORUNNING
    N: D.CHRONORUNNING
    T: 0.01
    O: g.increaseChronoByOne()
    
TRANSITION:
    S: D.CHRONORUNNING
    N: D.CHRONOSTOPPED
    C: [INSTATE("C.CHRONODISPLAY")]
    E: initChrono
    O: print "Stop chrono"
    
TRANSITION:
    S: D.CHRONORUNNING
    N: D.CHRONORUNNING
    C: [INSTATE("C.CHRONODISPLAY")]
    E: resetChrono
    O: g.resetChrono()
    
TRANSITION:
    S: D.CHRONOSTOPPED
    N: D.CHRONOSTOPPED
    C: [INSTATE("C.CHRONODISPLAY")]
    E: resetChrono
    O: g.resetChrono()                 
    
                                                                    